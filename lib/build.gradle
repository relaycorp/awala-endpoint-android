plugins {
  id 'com.android.library'
  id 'kotlin-android'
  id 'kotlin-kapt'
  id 'maven-publish'
  id 'org.jlleitschuh.gradle.ktlint' version "10.2.0"
  id 'org.jetbrains.dokka' version "1.5.31"
}

apply from: 'jacoco.gradle'

android {
  compileSdkVersion 31
  buildToolsVersion "30.0.3"

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 31
    versionCode 1
    versionName "1.0.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
  }

  buildTypes {
    debug {
      testCoverageEnabled = true
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    coreLibraryDesugaringEnabled true
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8
    freeCompilerArgs += "-Xexplicit-api=strict"
  }
  testOptions {
    unitTests {
      returnDefaultValues = true
      includeAndroidResources = true
    }
  }
  packagingOptions {
    exclude 'META-INF/AL2.0'
    exclude 'META-INF/LGPL2'
    exclude 'META-INF/LGPL2.1'
    exclude 'META-INF/licenses/*'
    exclude '**/attach_hotspot_windows.dll'
  }
}

dependencies {
  // Java 8
  coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

  // Kotlin
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"

  // Awala
  implementation 'tech.relaycorp:awala:1.50.0'
  implementation 'tech.relaycorp:poweb:1.5.24'

  // Security
  implementation 'androidx.security:security-crypto:1.1.0-alpha03'
  implementation "org.bouncycastle:bcpkix-jdk15on:1.69"

  // Serialization
  implementation 'org.mongodb:bson:4.3.3'

  // Testing
  testImplementation 'junit:junit:4.13.2'
  testImplementation 'androidx.test:core:1.4.0'
  testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutinesVersion"
  testImplementation 'org.robolectric:robolectric:4.6.1'
  testImplementation 'org.mockito:mockito-inline:4.0.0'
  testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
  testImplementation 'tech.relaycorp:awala-testing:1.2.4'

  // Instrumentation Testing
  androidTestImplementation 'androidx.test:runner:1.4.0'
  androidTestImplementation 'androidx.test:rules:1.4.0'
  androidTestImplementation 'androidx.test.ext:junit:1.1.3'
  androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutinesVersion"
  androidTestImplementation 'org.mockito:mockito-android:4.0.0'
  androidTestImplementation("com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0") {
    // Workaround for Android API < 26:
    // https://github.com/mockito/mockito/pull/2024
    exclude group: "org.mockito", module: "mockito-core"
  }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8
    freeCompilerArgs += [
      '-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi',
      '-Xuse-experimental=kotlinx.coroutines.FlowPreview',
      '-Xuse-experimental=kotlin.time.ExperimentalTime',
      '-Xuse-experimental=io.ktor.util.KtorExperimentalAPI'
    ]
  }
}

dokkaHtml.configure {
    dokkaSourceSets {
        configureEach {
            reportUndocumented.set(true)
            includes.from("api-docs.md")
        }
    }
}

ktlint {
  verbose = true
  android = true
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'tech.relaycorp'
                artifactId = 'awaladroid'

                pom {
                    name.set(rootProject.name)
                    description.set("High-level library for Android apps implementing Awala endpoints")
                    url.set("https://github.com/relaycorp/relaynet-endpoint-android")
                    developers {
                        developer {
                            id.set("relaycorp")
                            name.set("Relaycorp, Inc.")
                            email.set("no-reply@relaycorp.tech")
                        }
                    }
                    licenses {
                        license {
                            name.set("Apache-2.0")
                        }
                    }
                    scm {
                        connection.set("scm:git:https://github.com/relaycorp/relaynet-endpoint-android.git")
                        developerConnection.set(
                            "scm:git:https://github.com/relaycorp/relaynet-endpoint-android.git"
                        )
                        url.set("https://github.com/relaycorp/relaynet-endpoint-android")
                    }
                }
            }
        }
    }
}
