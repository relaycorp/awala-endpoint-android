jacoco {
    toolVersion = "0.8.6"
}

def jacocoFiles = files([
        fileTree(
                dir: project.buildDir,
                includes: ["classes/kotlin/main/"],
        )
])

jacocoTestReport {
    classDirectories.from = jacocoFiles

    reports {
        xml.enabled = true
        html.enabled = true
        html.destination = file("$buildDir/reports/coverage")
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.from = jacocoFiles
    }

    violationRules {
        rule {
            limit {
                counter = "CLASS"
                value = "MISSEDCOUNT"
                // Workaround for https://github.com/jacoco/jacoco/issues/654
                maximum = "1".toBigDecimal()
            }

            limit {
                counter = "METHOD"
                value = "MISSEDCOUNT"
                // Workaround for https://github.com/jacoco/jacoco/issues/654
                maximum = "1".toBigDecimal()
            }

            limit {
                counter = "BRANCH"
                value = "MISSEDCOUNT"
                // Workaround for https://github.com/jacoco/jacoco/issues/1036
                maximum = "13".toBigDecimal()
            }
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
    finalizedBy("jacocoTestReport")
    doLast {
        println("View code coverage at:")
        println("file://$buildDir/reports/coverage/index.html")
    }
}
